#!/usr/bin/env python

Import("env")

iphone_lib = [
    "#platform/uikit/uikit_app_delegate.m",
    "#platform/uikit/uikit_display_layer.mm",
    "#platform/uikit/uikit_joypad.mm",
    "#platform/uikit/uikit_os.mm",
    "#platform/uikit/uikit_view_controller.mm",
    "#platform/uikit/uikit_view_renderer.mm",
    "#platform/uikit/uikit_view.mm",
    "godot_iphone.mm",
    "os_iphone.mm",
    "main.m",
    "app_delegate.mm",
    "ios.mm",
    "godot_view.mm",
    "godot_app_delegate.m",
    "godot_view_controller.mm",
    "godot_view_renderer.mm",
    "godot_view_gesture_recognizer.mm",
    "device_metrics.m",
    "keyboard_input_view.mm",
    "native_video_view.m",
]

env_ios = env.Clone()
ios_lib = env_ios.add_library("iphone", iphone_lib)

# (iOS) Enable module support
env_ios.Append(CCFLAGS=["-fmodules", "-fcxx-modules"])


def combine_libs(target=None, source=None, env=None):
    lib_path = target[0].srcnode().abspath
    if "osxcross" in env:
        libtool = "$IPHONEPATH/usr/bin/${ios_triple}libtool"
    else:
        libtool = "$IPHONEPATH/usr/bin/libtool"
    env.Execute(
        libtool + ' -static -o "' + lib_path + '" ' + " ".join([('"' + lib.srcnode().abspath + '"') for lib in source])
    )


combine_command = env_ios.Command("#bin/libgodot" + env_ios["LIBSUFFIX"], [ios_lib] + env_ios["LIBS"], combine_libs)
